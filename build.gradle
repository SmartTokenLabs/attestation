buildscript {
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.1.0'
    }
}

plugins {
    id 'java'
    id "com.github.node-gradle.node" version "3.0.0-rc5"
}

node {
    download = true
    version = "14.15.3"
}

apply plugin: 'com.github.johnrengelman.shadow'

if(JavaVersion.current() < JavaVersion.VERSION_11){
    throw new GradleException("This build must be run with java 11 or higher.")
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenCentral()
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-api:2.13.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.13.0'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.66'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.66'
    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j18-impl:2.13.0'
    runtimeOnly 'com.amazonaws:aws-lambda-java-log4j2:1.2.0'
    implementation('com.amazonaws:aws-lambda-java-core:1.2.0')
    implementation('com.amazonaws:aws-lambda-java-events:2.2.6')
    implementation 'org.json:json:20200518'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    intTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    intTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    implementation 'org.web3j:core:4.6.0'
    implementation group: 'com.github.cliftonlabs', name: 'json-simple', version: '3.1.0'
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.4'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
            exclude 'id' // the code is being reworked on
            exclude 'dk/alexandra/stormbird/cheque' // the code depends on objsys library
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
            exclude 'id' // the code is being reworked on
            exclude 'dk/alexandra/stormbird/cheque' // the code depends on objsys library
        }
    }
    intTest {
        java {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
	}
    }
}

test {
    useJUnitPlatform()
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'com.alphawallet.attestation.demo.Demo'
    }
}

task packageFat(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}

task packageLibs(type: Jar) {
    into('java/lib') {
        from configurations.runtimeClasspath
    }
    dirMode = 0755
    fileMode = 0755
}

// exclude bunch of com/alphawallet/â€¦ but include token/entity and Signable class
task packageAttestation(type: Jar) {
    from compileJava
    from processResources
    exclude {
        if(it.relativePath.pathString.startsWith('com/alphawallet/attestation/demo')
           || it.relativePath.pathString.startsWith('com/alphawallet/ethereum')
           || it.relativePath.pathString.startsWith('com/alphawallet/token/tools')
           || it.relativePath.pathString.startsWith('com/alphawallet/token/util')
           || it.relativePath.pathString.startsWith('id/attestation')
           || it.relativePath.pathString.startsWith('dk')){
            return true
        } else if(!it.directory
            && it.relativePath.pathString.startsWith('com/alphawallet/token/entity')
            && it.name != 'Signable.class'){
            return true
        }
    }
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

task testJavaScript(type: NodeTask) {
    dependsOn 'test'
    dependsOn 'npmInstall'
    script = file('src/test/javascript/ParserSerializerTest.js')
}

build.dependsOn packageAttestation
